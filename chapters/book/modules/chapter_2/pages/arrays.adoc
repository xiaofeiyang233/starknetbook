[id="arrays"]

= Cairo 中的 Array、Span、Snapshot

====
在完成本小节后，我们强烈建议通过参考 https://cairo-book.github.io/ch03-00-understanding-ownership.html[Cairo Book] 来探讨 Cairo 中的所有权概念。所有权是一个至关重要的概念，受到 Rust 的启发并继承自 Rust，它主导资源管理并防止常见的编程错误，如双重释放、使用后释放和数据竞争。掌握 Cairo 中的所有权概念对于开发健壮且安全的智能合约以及成为熟练的 Cairo 开发者至关重要。
====

Cairo 中的 Array（数组）是一种将同一类型的多个值顺序存储在内存中的集合。与 Rust 相似，Cairo 数组是只追加的，这意味着你只能将元素添加到数组的末尾。一旦内存插槽被写入，就无法覆盖它，但你仍然可以从中读取。

Array（数组） 对于需要固定大小或动态增长的数据结构的数据存储和管理非常有用。

要在 Cairo 中使用 Array（数组），你需要导入 array::ArrayTrait trait：

[source, rust]
----
use array::ArrayTrait; // import the ArrayTrait trait
----

我们将使用一个宝可梦示例来说明这些概念。你可以在 https://github.com/starknet-edu/starknetbook/blob/main/chapters/modules/chapter_2/pages/contracts/src/pokemon_array.cairo[Book repository] 的 contracts 文件夹中找到文档化的合约代码。

== 创建和初始化 Array（数组）

要在 Cairo 中创建数组，你可以使用 `ArrayTrait::new()` 方法。数组默认是可变的，这意味着你可以修改它们的内容。

以下是创建一个包含宝可梦名称的数组并向其追加元素的示例（请记住实际合约位于 https://github.com/starknet-edu/starknetbook/blob/main/chapters/modules/chapter_2/pages/contracts/src/pokemon_array.cairo[Book repository]）：

[source, rust]
----
fn create_pokemon_array() -> Array::<felt252> {
    let mut pokemons = ArrayTrait::new(); // 创建一个类型为 felt252 的新数组
    pokemons.append('Pikachu'); //  将 'Pikachu' 追加到 pokemons 数组
    pokemons.append('Charmander');
    pokemons.append('Bulbasaur');
    pokemons.append('Squirtle');
    pokemons // 返回数组
}
----

In this example:

* `let mut pokemons = ArrayTrait::new()` 创建一个类型为 `felt252` 的新可变数组。
* 使用 `append()` 方法将元素（宝可梦名称）添加到数组。
* 函数在追加宝可梦名称后返回一个数组。

在字符串章节中，我们回顾了处理短字符串（例如，`'Pikachu'` ）的合约部分。

=== 重点概念

* 使用 `ArrayTrait` 特性处理 Cairo 中的数组。
* 数组默认是可变的。
* 使用 `append()` 方法将元素添加到数组。
* 数组可用于以顺序方式存储和管理元素集合。
* 数组是只追加的，这意味着你只能将元素添加到数组的末尾。
* 数组的索引从 0 开始。

== 删除元素和获取数组长度

在 Cairo 中，你可以使用 `pop_front()` 方法从数组前面删除元素。如果数组不为空，此方法将返回一个包含已删除值的 `Option` ，如果数组为空，则返回 `None`。需要注意的是，在 Cairo 中只能从数组的前面删除元素。

要获取数组的长度，你可以使用 `len()` 方法。

以下是删除元素和获取宝可梦数组长度的示例：

[source, rust]
----
fn pop_pokemon_array() {
    let mut pokemons: Array<felt252> = create_pokemon_array();
    assert(pokemons.len() == 4_usize, "Unexpected length");

    let first_pokemon = pokemons.pop_front().unwrap();
    assert(pokemons.len() == 3_usize, "Unexpected length");
    assert(first_pokemon == 'Pikachu', "Unexpected pokemon");
}
----

In this example:

* `pokemons.len()` 用于获取 `pokemons` 数组的长度。
* `pokemons.pop_front()` 用于从 `pokemons` 数组中删除第一个元素（前端）。
* `unwrap()` 用于从 `pop_front()` 返回的 Option 中获取已删除的值。
* 断言检查数组长度和删除的元素是否符合预期。


== 访问数组元素

要访问数组中的元素，你可以使用 `get()` 和 `at()` 方法。`get()` 方法返回一个 `Option`，如果索引有效，则包含该值，如果索引越界，则为 None。`at()` 方法直接返回指定索引处的值，但如果索引越界，它将导致错误。

这是一个访问宠物小精灵数组元素的示例：

[source, rust]
----
fn index_pokemon_array() {
    let mut pokemons: Array<felt252> = create_pokemon_array();

    assert(*pokemons.get(0_usize).unwrap().unbox() == 'Pikachu', "Wrong pokemon");
    assert(*pokemons.at(1_usize) == 'Charmander', "Wrong pokemon");
}
----

== 理解 Snapshot 与 Span
在本章中，我们将探讨 Starknet 中 `Snapshot`（快照）和 `Span`（跨度）的概念。`Span` 是一个轻量级的数组视图，允许你访问底层数据而不修改原始数组。你可以使用 `span()` 方法从数组中创建一个 `Span` 。在深入了解 `Span` 之前，了解 `Snapshot`（快照）（由运算符 @ 定义）是至关重要的，因为 Span 结构是基于它们构建的

[source, rust]
----
struct Span<T> {
    snapshot: @Array<T>
}
----

一个 `T` 值的 `Span`（例如，Span<felt252>）是一个具有一个元素的结构：一个相同值 T（例如，Array<felt252>）的 Array 的快照。

=== Snapshot

Cairo 中的快照是一个强大的功能，提供了在不占用所有权的情况下将对象的只读实例传递给函数。每种类型 T 都有一个快照版本 @T，它总是可以丢弃和复制的

要创建类型为 `T` 的值 `x` 的快照，只需使用 `@x`。


==== Snapshot 与 Rust 引用的对比

虽然 Snapshot（快照）和 Rust 引用（`&`）乍一看似乎相似，但它们之间存在关键差异：

1. **对象类型**: 与 Rust 引用不同，后者是指针，Snapshot（快照）是完整的对象。因此，使用 Snapshot（快照）不会产生与指针使用相关的性能改进。
2. **对象不可变性**: 当在创建 Snapshot（快照）后修改对象时，Snapshot（快照）保持不变。这种行为与 Rust 引用形成对比，后者会反映底层对象的更改。

==== Snapshot（快照）的实际用途

Snapshot（快照）在各种用例中有很多用途，特别是在使用 Cairo 中的数组和数据结构时。以下是一些 Snapshot（快照）的实际示例：

* **保留原始数据**: 当编写一个接受数组并返回前两个元素之和的函数时，你可能不希望修改原始数组。在这种情况下，使用数组的 Snapshot（快照）作为参数而不是实际数组可以保留原始数据。
* **不变数据视图**: 如果你对一个数组进行 Snapshot（快照），然后向原始数组添加一个值，Snapshot（快照）将保持不受影响，保持原始值而不包括新添加的值。当你需要在特定时间点查看不可变数据时，此功能非常有用。
* **只读实例**: 一些函数，如 `Array::get` 和 `Array::at`，需要对象的 Snapshot（快照）而不是对象本身。通过使用快照，你可以在调用函数时创建不拥有对象的只读实例。当你需要访问数据而不修改底层数据结构时，这种方法非常有帮助。

总之，Snapshot（快照）使开发者能够更高效地处理数据，提供了保留原始数据、创建不可变视图和访问只读实例而不修改原始对象的方法。

=== Spans

Span 提供了一种方便的方法来处理数组，而无需修改原始数组或担心所有权。要使用 span，必须导入 `array::SpanTrait trait`：

[source, rust]
----
use array::SpanTrait;
----

考虑以下示例，它演示了如何在 Pokémon span 中访问元素：

[source, rust]
----
fn index_pokemon_span() {
    let pokemons_span: Span<felt252> = create_pokemon_array().span();

    assert(*pokemons_span.get(2_usize).unwrap().unbox() == 'Bulbasaur', "Wrong pokemon");
    assert(*pokemons_span.at(3_usize) == 'Squirtle', "Wrong pokemon");
}
----

SpanTrait 中的 `get` 和 `at` 函数都期望使用快照。如果 Pokémon 合约使用 `pokemons.at(1_usize)` 而不是快照 `*pokemons.at(1_usize)`，编译器将返回错误：

[source, rust]
----
error: Plugin diagnostic: Unexpected argument type. Expected: "@core::felt252", found: "core::felt252".
 --> pokemon_array.cairo:41:40
        assert(pokemons.at(1_usize) == "Charmander", "Wrong pokemon");
                                       ^**********^
----

在这种情况下，`at` 方法期望快照（`@core::felt252`）而不是实际值（`core::felt252`）。

为了避免这类错误并编写高效的智能合约，Cairo 开发者应对所有权有深入了解，这受到 Rust 的启发。请参阅 https://cairo-book.github.io/ch03-00-understanding-ownership.html[Cairo Book]，了解有关此主题的更多信息。


== 结论和总结

在本章中，我们深入探讨了 Cairo 中的数组世界，以 Pokémon 为例使概念更具亲和力和趣味性。我们学习了如何创建数组、添加元素、删除元素以及获取数组的长度。简要回顾一下，我们介绍了以下内容：


* *创建数组*: 使用 `ArrayTrait::new()` 创建特定类型的新可变数组。
* *添加元素*: 使用 `append()` 方法将元素添加到数组中。请记住，Cairo 数组是只追加的。
* *删除元素*: 在 Cairo 中，你只能从数组前端删除元素。使用 `pop_front()` 方法，它返回一个包含已删除值的 `Option` 或者在数组为空时返回 `None`。
* *获取数组长度*: 使用 `len()` 方法获取数组的长度。
* *访问元素*: 使用 `get()` 和 `at()` 方法访问数组中的元素。`get()` 方法返回一个 `Option`，如果索引有效则包含值，如果索引超出范围则返回 `None`。`at()` 方法直接返回指定索引处的值，但如果索引超出范围，它将导致错误。

在接下来的子章节中，我们将通过将 Pokemon 合约部署到 Starknet 测试网来测试我们的断言。这是测试合约的最糟糕的方法，但在目前的知识水平下，这是唯一的测试方法。在下一章中，我们将回顾 Protostar 和 Cairo 测试框架，这将使我们能够在本地测试合约。


[NOTE]
====
《Starknet 之书》是 Starknet 社区成员合力之作，便于社区成员学习之用。

* 无论你是否有所收获，烦请填写此问卷， https://a.sprig.com/WTRtdlh2VUlja09lfnNpZDo4MTQyYTlmMy03NzdkLTQ0NDEtOTBiZC01ZjAyNDU0ZDgxMzU=[简单回答三个问题] ，给予我们反馈。
* 若发现任何错误，或有其他建议，请在我们的 https://github.com/starknet-edu/starknetbook/issues[Github 仓库]发起问题单 (Issues)。
====

== Contributing

[quote, The Starknet Community]
____
*Unleash Your Passion to Perfect StarknetBook*

StarknetBook is a work in progress, and your passion, expertise, and unique insights can help transform it into something truly exceptional. Don't be afraid to challenge the status quo or break the Book! Together, we can create an invaluable resource that empowers countless others.

Embrace the excitement of contributing to something bigger than ourselves. If you see room for improvement, seize the opportunity! Check out our https://github.com/starknet-edu/starknetbook/blob/main/CONTRIBUTING.adoc[guidelines] and join our vibrant community. Let's fearlessly build Starknet! 
____
